apply plugin: 'maven'
apply plugin: 'signing'

ext {
    if (!project.hasProperty("ossrhUsername"))
        ossrhUsername = ""
    if (!project.hasProperty("ossrhPassword"))
        ossrhPassword = ""
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                packaging 'jar'
                url 'https://github.com/SpinlockLabs/Singularity'

                scm {
                    connection 'scm:git:git://github.com/SpinlockLabs/Singularity.git'
                    developerConnection 'scm:git:git@github.com:SpinlockLabs/Singularity.git'
                    url 'https://github.com/SpinlockLabs/Singularity'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'logangorence'
                        name 'Logan Gorence'
                        email 'loganjohngorence@gmail.com'
                    }
                }
            }
        }
    }
}
